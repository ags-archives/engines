//===================================================================
// *** AGS ROOM SCRIPT ***
//
// Project: Demo Quest 3 
//          Source of Dreams
//
// Room:    Dar's Street
//              
// Author:  Allan Menefe (Holister_Man)
//          Rick Jafrate (RickJ)    
//
// Abstract
// This is an early room of the game.  Interactions that only write a
// message to the screen are handled with the Interaction and Message 
// Editors. More complicated interactions are implemented with script
// commands placed in the Room Script or Global Script.  
// 
// Description
// There  is  an adult establishment with a neon sign that is need of
// repair. The sign intermitteninly blinks off and on.  The player is
// barred from entering because "No dames are allowed in here!!" as 
// informed by the bouncer, Pirate Bob. Perhaps that Gend-R-matic 
// machine in the back alley could be of use?  
//
// Scripting Hint - Script Commands vs Interaction Editor
// The Interaction Editor  gives access to a subset of AGS' commands.
// Simple  things  are  easily done using the Interaction Editor, For 
// those  new  to programming  languages this is a good way to start. 
// However, as interactions become more  complicated, it becomes more
// difficult to implement the desired behaviour using the Interaction
// Editor.  A  more  flexible  alternative  to  exclusively using the 
// Interaction  Editor  is  to run a series of script commands.  Both 
// methods are often used together.
//
// ToDo
// - Chris's Gender Transformation 
// - Transition to Gend-R-Matic room
//
// Revision History
// 05-Mar-15 RickJ,        update for AGS 2.7
// 04-Dec-01 Holister_Man, original version
// 
// Copyright (c) 2005 by Chris Jones
//-------------------------------------------------------------------


//===================================================================
// Constant Definitions
//-------------------------------------------------------------------
#define SIGN_MAXON		159
#define SIGN_MAXOFF		3


//===================================================================
// Static Variable Definitions
//-------------------------------------------------------------------
int SignOnCounter=0;
int SignOffCounter=0;
int EnableExit=false;

//===================================================================
// Utility Functions
//-------------------------------------------------------------------


//===================================================================
// Application Functions
//-------------------------------------------------------------------

//===================================================================
   function SignFlicker() {
//
// This function is called from the "repeatedly execute" interaction.
// It  makes  the  neon  sign flicker by randomly changing the room's
// background frame to one of  two  images.  Two  counters are set to 
// random  values. The first counter, SignOnTimer, is  decremented to 
// zero.  During  this  time the sign is  turned on.  Next the second 
// counter is decremented to  zero.  During  this  time  the  sign is 
// turned off. When both counters are zero they are set to new random
// values and the whole proces repeats itself.
//-------------------------------------------------------------------
	if (SignOnCounter>0) {
		SetBackgroundFrame(0); 					// Turn sign on
		SignOnCounter--;							// Decrement first counter
	}
	else if (SignOffCounter>0) {
		SetBackgroundFrame(1); 					// Turn sign off
		SignOffCounter--;							// Decrement second counter
	} 
	else {
		SignOnCounter = Random(SIGN_MAXON)+1;
		SignOffCounter = Random(SIGN_MAXOFF)+1;
	}
}

//===================================================================
   function EnterDars() { 
//
// This  function transports  the player to a closeup of the door and
// building where a conversation ensues with Pirate  Bob, the bouncer.
// He  explains that partons must present an ID card and be male. The
// puzzle is implemented in the new room.
//-------------------------------------------------------------------
	player.Walk(185,116,eBlock);
	PlaySound(sndKnock01);
	Wait(20);
	player.ChangeRoom(crmDarsDoor,159,199);
}

//===================================================================
// Room - Interaction Handler Functions
//-------------------------------------------------------------------
#sectionstart room_a  // DO NOT EDIT OR REMOVE THIS LINE
function room_a() {
	// script for room: Player enters screen (before fadein)

	// Disable player exit room
	EnableExit = false;
	
	// Help GUI
	gHelpTitle.Text = "Dar's Street";
	gHelpDescription.Text = "Use background animation to make sign flicker, animate an object to open a door, and conduct dialog with another characer.";
	gHelpReference.Text = "Manual Reference:[Animating background scenes, Animate (object), SetView, Random";  
}
#sectionend room_a  // DO NOT EDIT OR REMOVE THIS LINE

#sectionstart room_c  // DO NOT EDIT OR REMOVE THIS LINE
function room_c() {
	// script for room: Repeatedly execute
   SignFlicker();
}
#sectionend room_c  // DO NOT EDIT OR REMOVE THIS LINE

#sectionstart region6_a  // DO NOT EDIT OR REMOVE THIS LINE
function region6_a() {
	// script for region6: Player walks onto region
	// if (EnableExit) player.ChangeRoom(crmTrashCanStreet,20,150);
	player.ChangeRoom(crmTrashCanStreet,20,150);
}
#sectionend region6_a  // DO NOT EDIT OR REMOVE THIS LINE

#sectionstart region5_a  // DO NOT EDIT OR REMOVE THIS LINE
function region5_a() {
	// script for region5: Player walks onto region
	// if (EnableExit) player.ChangeRoom(crmTrashCanStreet,20,110);
	player.ChangeRoom(crmTrashCanStreet,20,110);
}
#sectionend region5_a  // DO NOT EDIT OR REMOVE THIS LINE

#sectionstart region1_a  // DO NOT EDIT OR REMOVE THIS LINE
function region1_a() {
  // script for region1: Player walks onto region
	EnableExit = true;  
}
#sectionend region1_a  // DO NOT EDIT OR REMOVE THIS LINE


//===================================================================
// Dar's - Interaction Handler Functions
//-------------------------------------------------------------------
#sectionstart object0_a  // DO NOT EDIT OR REMOVE THIS LINE
function object0_a() {
	// script for object0: Interact object
	EnterDars();
}
#sectionend object0_a  // DO NOT EDIT OR REMOVE THIS LINE


#sectionstart room_d  // DO NOT EDIT OR REMOVE THIS LINE
function room_d() {
	// script for Room: First time player enters screen
	cEgo.SayBackground("Press F1 for help");
  
}
#sectionend room_d  // DO NOT EDIT OR REMOVE THIS LINE


function hSportsCar_Look()
{
DisplayMessage(0);
}

function hSportsCar_Interact()
{
DisplayMessage(13);
}

function hFence_Look()
{
DisplayMessage(1);
}

function hRoad_Look()
{
DisplayMessage(2);
}

function hSkyline_Look()
{
DisplayMessage(3);
}

function hSidewalk_Look()
{
DisplayMessage(4);
}

function hAztecBuilding_Look()
{
DisplayMessage(5);
}

function hWindow_Look()
{
DisplayMessage(6);
}

function hWindow_Interact()
{
DisplayMessage(9);
}

function hNeonSign_Look()
{
DisplayMessage(7);
}

function hNeonSign_Interact()
{
DisplayMessage(11);
}

function hDoor_Look()
{
DisplayMessage(8);
}

function hDoor_Interact()
{
DisplayMessage(8);
}

function hBuilding_Look()
{
DisplayMessage(10);
}

function hTrashCans_Look()
{
DisplayMessage(12);
}

function hTrashCans_Interact()
{
DisplayMessage(14);
}
